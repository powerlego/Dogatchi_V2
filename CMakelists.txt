cmake_minimum_required(VERSION 3.20)

project(dogatchi)

if(WIN32)
    SET(CMAKE_WINDOWS_EXPORT_ALL_SYMBOLS TRUE)
endif()

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

function(add_boost_test SOURCE_FILE_NAME DEPENDENCY_LIB INCLUDE_DIRS)
    get_filename_component(TEST_EXECUTABLE_NAME ${SOURCE_FILE_NAME} NAME_WE)

    add_executable(${TEST_EXECUTABLE_NAME} ${SOURCE_FILE_NAME})
    target_link_libraries(${TEST_EXECUTABLE_NAME}
        ${DEPENDENCY_LIB} ${Boost_UNIT_TEST_FRAMEWORK_LIBRARY} Boost::thread)
    target_include_directories(${TEST_EXECUTABLE_NAME} PRIVATE ${INCLUDE_DIRS})

    file(READ "${SOURCE_FILE_NAME}" SOURCE_FILE_CONTENTS)
    string(REGEX MATCHALL "BOOST_AUTO_TEST_CASE\\( *([A-Za-z_0-9]+) *\\)"
        FOUND_TESTS ${SOURCE_FILE_CONTENTS})

    foreach(HIT ${FOUND_TESTS})
        string(REGEX REPLACE ".*\\( *([A-Za-z_0-9]+) *\\).*" "\\1" TEST_NAME ${HIT})

        add_test(NAME "${TEST_EXECUTABLE_NAME}.${TEST_NAME}"
            COMMAND ${TEST_EXECUTABLE_NAME}
            --run_test=${TEST_NAME} --catch_system_error=yes)
    endforeach()
endfunction()

set(library_name lib${PROJECT_NAME})

find_package(Doxygen COMPONENTS dot)

if(DOXYGEN_FOUND)
    set(DOXYGEN_BUILTIN_STL_SUPPORT YES)

    if(WIN32)
        set(DOXYGEN_CPP_CLI_SUPPORT YES)
    endif()

    set(DOXYGEN_EXTRACT_STATIC YES)
    set(DOXYGEN_SOURCE_BROWSER YES)
    set(DOXYGEN_HTML_COLORSTYLE "AUTO_DARK")
    set(DOXYGEN_HTML_DYNAMIC_SECTIONS YES)
    set(DOXYGEN_GENERATE_TREEVIEW YES)
    set(DOXYGEN_DISABLE_INDEX YES)
    set(DOXYGEN_DOT_IMAGE_FORMAT svg)
    set(DOXYGEN_INTERACTIVE_SVG YES)
    set(DOXYGEN_UML_LOOK YES)
    set(DOXYGEN_UML_LIMIT_NUM_FIELDS 50)
    set(DOXYGEN_UML_DETAILS YES)
    set(DOXYGEN_CALL_GRAPH YES)
    set(DOXYGEN_CALLER_GRAPH YES)
    set(DOXYGEN_DOT_MULTI_TARGETS YES)
    set(DOXYGEN_GENERATE_LEGEND NO)
endif(DOXYGEN_FOUND)

find_package(Boost REQUIRED COMPONENTS unit_test_framework thread system)

include_directories(${Boost_INCLUDE_DIRS})

include(CTest)

add_subdirectory(lib)
add_subdirectory(app)
add_subdirectory(test)